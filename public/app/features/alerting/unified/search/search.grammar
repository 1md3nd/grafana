@top AlertRuleSearch { expression+ }

expression { FilterExpression | FreeFormExpression }

FreeFormExpression { word | stringWithQuotes }

FilterExpression {
 filter<DataSourceToken> |
 filter<NameSpaceToken> |
 filter<LabelToken> |
 filter<GroupToken> |
 filter<RuleToken> |
 filter<StateToken> |
 filter<TypeToken> |
 filter<HealthToken> |
 filter<DashboardToken>
}

filter<token> { token FilterValue }

@tokens {
  colon { ":" }

  // Special characters (except colon, quotes and space), Latin characters, extended latin and emoji
  allowedInputChar { $[!#$%&'()*+,-./] | $[\u{0030}-\u{0039}] | $[\u{003b}-\u{1eff}] | $[\u{2030}-\u{1faff}] }
  word { allowedInputChar+ }

  allowedInputCharOrColon { allowedInputChar | colon }
  allowedInputCharOrColonOrWhitespace { allowedInputCharOrColon | @whitespace }
  stringWithQuotes { ("\"" allowedInputCharOrColonOrWhitespace+ "\"") }

  FilterValue { allowedInputCharOrColon+ | stringWithQuotes }
  filterToken<type> { type colon }

  DataSourceToken { filterToken<"datasource"> }
  NameSpaceToken { filterToken<"namespace"> }
  LabelToken { filterToken<"label"> }
  GroupToken { filterToken<"group"> }
  RuleToken { filterToken<"rule"> }
  StateToken { filterToken<"state"> }
  TypeToken { filterToken<"type"> }
  HealthToken { filterToken<"health"> }
  DashboardToken { filterToken<"dashboard"> }

  @precedence { DataSourceToken, word }
  @precedence { NameSpaceToken, word }
  @precedence { LabelToken, word }
  @precedence { GroupToken, word }
  @precedence { RuleToken, word }
  @precedence { StateToken, word }
  @precedence { TypeToken, word }
  @precedence { HealthToken, word }
  @precedence { DashboardToken, word }
}
